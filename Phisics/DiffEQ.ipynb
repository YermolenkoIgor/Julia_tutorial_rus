{
 "cells": [
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## ФАЗОВЫЕ ПОРТРЕТЫ ДИНАМИЧЕСКИХ СИСТЕМ\n",
    "\n",
    "https://habr.com/ru/post/428984/\n",
    "\n",
    "Решения обыкновенных дифференциальных уравнений часто удобнее изображать не в привычном виде $y^1(t),...,y^L(t)$, а в *фазовом пространстве*, по осям которого откладываются значения каждой из найденных функций. При этом аргумент t входит в графики лишь параметрически. В случае двух ОДУ такой график – фазовый портрет системы, является кривой на фазовой плоскости и поэтому особенно нагляден.\n",
    "\n",
    "### Осциллятор\n",
    "\n",
    "Динамика гармонического осциллятора с затуханием $\\gamma$ описывается следующей системой уравнений:\n",
    "\n",
    "$$\n",
    "\\dot{x}=y\\\\\n",
    "\\dot{y}=-\\omega^2x+\\gamma y\n",
    "$$\n",
    "\n",
    "и независимо от начальных условий  (х0, у0), приходит в состояние равновесия, точку (0,0) с нулевым углом отклонения и нулевой скоростью.\n",
    "При $\\gamma=0$ решение принимает вид свойственный для классического осциллятора."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "using DifferentialEquations, Gadfly"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "# using Plots\n",
    "# plotly()"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "function garmosc(ω = pi, γ = 0, x0 = 0.1, y0 = 0.1)\n",
    "\n",
    "    A = [0 1\n",
    "        -ω^2 γ]\n",
    "\n",
    "    syst(u,p,t) = A * u # ODE system\n",
    "\n",
    "    u0 = [x0, y0] # start cond-ns\n",
    "    tspan = (0.0, 4pi) # time period\n",
    "\n",
    "    prob = ODEProblem(syst, u0, tspan) # problem to solve\n",
    "    sol = solve(prob, RK4(),reltol=1e-6, timeseries_steps = 4)\n",
    "\n",
    "    N = length(sol.u)\n",
    "    J = length(sol.u[1])\n",
    "\n",
    "    U = zeros(N, J)\n",
    "\n",
    "    for i in 1:N, j in 1:J\n",
    "        U[i,j] = sol.u[i][j] # перепишем ответы в матрицу поудобней\n",
    "    end\n",
    "    sol.t, U\n",
    "end\n",
    "\n",
    "T, Ans0 = garmosc()\n",
    "\n",
    "p1 = plot(x = Ans0[:,1], y = Ans0[:,2], Geom.point)\n",
    "l1 = layer( x = T, y = Ans0[:,1], Geom.point, Theme(default_color=\"red\") )\n",
    "l2 = layer( x = T, y = Ans0[:,2], Geom.point, Theme(default_color=\"green\") )\n",
    "p2 = plot(l1, l2)\n",
    "vstack(p1, p2)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "function garmosc(ω = pi, γ = 0, x0 = 0.1, y0 = 0.1)\n",
    "\n",
    "    A = [0 1\n",
    "        -ω^2 γ]\n",
    "\n",
    "    syst(u,p,t) = A * u # ODE system\n",
    "\n",
    "    u0 = [x0, y0] # start cond-ns\n",
    "    tspan = (0.0, 2pi) # time period\n",
    "\n",
    "    prob = ODEProblem(syst, u0, tspan) # problem to solve\n",
    "    sol = solve(prob, RK4(),reltol=1e-6, timeseries_steps = 4)\n",
    "\n",
    "    N = length(sol.u)\n",
    "    J = length(sol.u[1])\n",
    "\n",
    "    U = zeros(N, J)\n",
    "\n",
    "    for i in 1:N, j in 1:J\n",
    "        U[i,j] = sol.u[i][j] # перепишем ответы в матрицу поудобней\n",
    "    end\n",
    "    U\n",
    "end\n",
    "\n",
    "set_default_plot_size(10cm, 10cm)\n",
    "\n",
    "Ans0 = garmosc()\n",
    "plot(x = Ans0[:,1], y = Ans0[:,2])"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "Поварьируем частоту, параметр затухания, начальную координату и начальную скорость."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "L = Array{Any}(undef, 3)# создали массив чего угодно (для слоев графиков)\n",
    "clr = [\"red\" \"green\" \"yellow\"]\n",
    "\n",
    "function ploter(Answ)\n",
    "    for i = 1:3\n",
    "        L[i] = layer(x = Answ[i][:,1], y = Answ[i][:,2], Geom.path, Theme(default_color=clr[i]) )\n",
    "    end\n",
    "    p = plot(L[1], L[2], L[3])\n",
    "end\n",
    "\n",
    "\n",
    "Ans1 = garmosc.( [pi 2pi 3pi] )# частоту\n",
    "p1 = ploter(Ans1)\n",
    "\n",
    "Ans2 = garmosc.( pi, [0.1 0.3 0.5] )# затухание\n",
    "p2 = ploter(Ans2)\n",
    "\n",
    "Ans3 = garmosc.( pi, 0.1, [0.2 0.5 0.8] ) \n",
    "p3 = ploter(Ans3)\n",
    "\n",
    "Ans4 = garmosc.( pi, 0.1, 0.1, [0.2 0.5 0.8] )\n",
    "p4 = ploter(Ans4)\n",
    "\n",
    "set_default_plot_size(16cm, 16cm)\n",
    "\n",
    "vstack( hstack(p1,p2), hstack(p3,p4) ) # слепим зоризонтально и вертикально"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "Теперь рассмотрим не малые колебания, а колебания произвольной амплитуды:\n",
    "\n",
    "$$\n",
    "\\dot{x}=y\\\\\n",
    "\\dot{y}=-sin(\\omega x)+\\gamma y\n",
    "$$\n",
    "\n",
    "График решения на фазовой плоскости не является эллипсом (что говорит о нелинейности осциллятора). Чем меньше мы будем выбирать амплитуду колебаний (т.е. начальные условия), тем меньше будет проявляться нелинейность (поэтому-то малые колебания маятника можно считать гармоническими). "
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "function ungarmosc(ω = pi, γ = 0, x0 = 0.1, y0 = 0.1)\n",
    "\n",
    "    function syst(du,u,p,t)\n",
    "        du[1] = u[2]\n",
    "        du[2] = -sin( ω*u[1] )+γ*u[2]\n",
    "    end\n",
    "    \n",
    "    u0 = [x0, y0] # start cond-ns\n",
    "    tspan = (0.0, 2pi) # time period\n",
    "\n",
    "    prob = ODEProblem(syst, u0, tspan) # problem to solve\n",
    "    sol = solve(prob, RK4(),reltol=1e-6, timeseries_steps = 4)\n",
    "\n",
    "    N = length(sol.u)\n",
    "    J = length(sol.u[1])\n",
    "\n",
    "    U = zeros(N, J)\n",
    "\n",
    "    for i in 1:N, j in 1:J\n",
    "        U[i,j] = sol.u[i][j] # перепишем ответы в матрицу поудобней\n",
    "    end\n",
    "    U\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "Ans1 = ungarmosc.( [pi 2pi 3pi] )\n",
    "p1 = ploter(Ans1)\n",
    "\n",
    "Ans2 = ungarmosc.( pi, [0.1 0.4 0.7] )\n",
    "p2 = ploter(Ans2)\n",
    "\n",
    "Ans3 = ungarmosc.( pi, 0.1, [0.1 0.5 0.9] )\n",
    "p3 = ploter(Ans3)\n",
    "\n",
    "Ans4 = ungarmosc.( pi, 0.1, 0.1, [0.1 0.5 0.9] )\n",
    "p4 = ploter(Ans4)\n",
    "\n",
    "vstack( hstack(p1,p2), hstack(p3,p4) )"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "### Брюсселятор\n",
    "\n",
    "Эта модель описывает некоторую автокаталитическую химическую реакцию, в которой определенную роль играет диффузия. Модель была предложена в 1968 г. Лефевром и Пригожиным\n",
    "\n",
    "$$\n",
    "\\dot{u}_1=-(\\mu+1)u_1+u^2_1u_2+1 \\\\\n",
    "\\dot{u}_2=\\mu u_1-u^2_1u_2\n",
    "$$\n",
    "\n",
    "Неизвестные функции $u_1(t),\\,u_2(t)$ отражают динамику концентрации промежуточных продуктов химической реакции. Параметр модели $\\mu$ имеет смысл исходной концентрации катализатора (третьего вещества). \n",
    "\n",
    "Более детально, эволюцию фазового портрета брюсселятора можно наблюдать, проводя расчеты с различным параметром $\\mu$. При его увеличении узел будет сначала постепенно смещаться в точку с координатами (1, $\\mu$), пока не достигнет *бифуркационного* значения $\\mu$=2. В этой точке происходит качественная перестройка портрета, выражающаяся в рождении предельного цикла. При дальнейшем увеличении $\\mu$ происходит лишь количественное изменение параметров этого цикла."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "function brusselator(μ = 0.1, u0 = [0.1; 0.1])\n",
    "    \n",
    "    function syst(du,u,p,t)\n",
    "        du[1] = -(μ+1)*u[1] + u[1]*u[1]*u[2] + 1\n",
    "        du[2] =      μ*u[1] - u[1]*u[1]*u[2]\n",
    "    end\n",
    "\n",
    "    tspan = (0.0, 10) # time period\n",
    "\n",
    "    prob = ODEProblem(syst, u0, tspan) # problem to solve\n",
    "    sol = solve(prob, RK4(),reltol=1e-6, timeseries_steps = 1)\n",
    "\n",
    "    N = length(sol.u)\n",
    "    J = length(sol.u[1])\n",
    "\n",
    "    U = zeros(N, J)\n",
    "\n",
    "    for i in 1:N, j in 1:J\n",
    "        U[i,j] = sol.u[i][j] # перепишем ответы в матрицу поудобней\n",
    "    end\n",
    "    U\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "L = Array{Any}(undef, 10)\n",
    "\n",
    "function ploter(Answ)\n",
    "    for i = 1:length(Answ)\n",
    "        L[i] = layer(x = Answ[i][:,1], y = Answ[i][:,2], Geom.path )\n",
    "    end\n",
    "    plot(L[1], L[2], L[3], L[4], L[5], L[6], L[7], L[8], L[9], L[10])\n",
    "end\n",
    "\n",
    "SC = [ [0 0.5], [0 1.5], [2.5 0], [1.5 0], [0.5 1], [1 0], [1 1], [1.5 2], [0.1 0.1], [0.5 0.2] ]\n",
    "\n",
    "Ans1 = brusselator.( 0.1, SC )\n",
    "Ans2 = brusselator.( 0.8, SC )\n",
    "Ans3 = brusselator.( 1.6, SC )\n",
    "Ans4 = brusselator.( 2.5, SC )\n",
    "p1 = ploter(Ans1)\n",
    "p2 = ploter(Ans2)\n",
    "p3 = ploter(Ans3)\n",
    "p4 = ploter(Ans4)\n",
    "\n",
    "set_default_plot_size(16cm, 16cm)\n",
    "\n",
    "vstack( hstack(p1,p2), hstack(p3,p4) )"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "### Модель Вольтера «хищник-жертва»\n",
    "\n",
    "Модель взаимодействия \"хищник-жертва\" независимо предложили в 1925-1927 гг. Лотка и Вольтерра. Два дифференциальных уравнения \n",
    "\n",
    "$$\n",
    "\\dot{x}=A_1x-D_{12}xy \\\\\n",
    "\\dot{y}=-D_2y+A_{21}xy\n",
    "$$\n",
    "\n",
    "моделируют временную динамику численности двух биологических популяций жертвы x  и хищника y. Предполагается, что жертвы размножаются с постоянной скоростью $A_1$, а их численность убывает вследствие поедания хищниками. Хищники же размножаются со скоростью, пропорциональной количеству пищи (с коэффициентом $A_{12}$) и умирают естественным образом (смертность определяется константой $D_2$). Модель замечательна тем, что в такой системе наблюдаются циклическое увеличение и уменьшение численности и хищника, и жертвы, так часто наблюдаемое в природе."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "function predatorvictim(A1 = 0.1, D2 = 1., D12 = 0.1, A21 = 0.1, u0 = [10. 4.] ) # слабое место - целые\n",
    "    \n",
    "    function syst1(du,u,p,t)\n",
    "        du[1] =  A1*u[1] - D12*u[1]*u[2]\n",
    "        du[2] = -D2*u[2] + A21*u[1]*u[2]\n",
    "    end\n",
    "\n",
    "    tspan = (0.0, 100) # time period\n",
    "\n",
    "    prob = ODEProblem(syst1, u0, tspan) # problem to solve\n",
    "    sol = solve(prob, RK4(),reltol=1e-6, timeseries_steps = 6)\n",
    "\n",
    "    N = length(sol.u)\n",
    "    J = length(sol.u[1])\n",
    "\n",
    "    U = zeros(N, J)\n",
    "\n",
    "    for i in 1:N, j in 1:J\n",
    "        U[i,j] = sol.u[i][j] # перепишем ответы в матрицу по-удобней\n",
    "    end\n",
    "    U\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "L = Array{Any}(undef, 3)\n",
    "clr = [\"green\" \"yellow\" \"magenta\"] # значения по-возрастанию: зеленый, желтый, розовый\n",
    "\n",
    "function ploter(Answ)\n",
    "    for i = 1:length(Answ)\n",
    "        L[i] = layer(x = Answ[i][:,1], y = Answ[i][:,2], Geom.path, Theme(default_color=clr[i]) )\n",
    "    end\n",
    "    plot( L[1], L[2], L[3] )\n",
    "end\n",
    "\n",
    "SC = [ [10 8], [10 4], [10 1.5] ]\n",
    "\n",
    "Ans5 = predatorvictim.( 0.1, 1., 0.1, 0.1, SC )\n",
    "Ans1 = predatorvictim.( [0.1 0.3 0.6])\n",
    "Ans2 = predatorvictim.( 0.1, [0.5 1.0 2.0] )\n",
    "Ans3 = predatorvictim.( 0.1, 1., [0.1 0.3 0.7])\n",
    "Ans4 = predatorvictim.( 0.1, 1., 0.1, [0.1 0.3 0.7] )\n",
    "p1 = ploter(Ans1)\n",
    "p2 = ploter(Ans2)\n",
    "p3 = ploter(Ans3)\n",
    "p4 = ploter(Ans4)\n",
    "p5 = ploter(Ans5)\n",
    "\n",
    "set_default_plot_size(20cm, 25cm)\n",
    "\n",
    "vstack( hstack(p1,p2), hstack(p3,p4), p5 )"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "Несложно убедиться, решая уравнения при различных начальных условиях, что фазовый портрет этой системы представляет собой концентрические замкнутые кривые, окружающие одну стационарную точку типа *центр*. Как видно, модельные колебания численности обеих популяций существенно зависят от начальных условий - после каждого периода колебаний  система возвращается  в ту же точку.\n",
    "\n",
    "Для учета внутревидовой конкуренции добавим слагаемое $-Bx^2$\n",
    "\n",
    "$$\n",
    "\\dot{x}=A_1x-D_{12}xy-Bx^2 \\\\\n",
    "\\dot{y}=-D_2y+A_{21}xy\n",
    "$$\n"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "function predatorvictim2( B = 1e-7, u0 = [10. 4.], A1 = 0.1, D2 = 1., D12 = 0.1, A21 = 0.1 ) # слабое место - целые\n",
    "    \n",
    "    function syst2(du,u,p,t)\n",
    "        du[1] =  A1*u[1] - D12*u[1]*u[2] - B*u[1]*u[1]\n",
    "        du[2] = -D2*u[2] + A21*u[1]*u[2] \n",
    "    end\n",
    "\n",
    "    tspan = (0.0, 100) # time period\n",
    "\n",
    "    prob = ODEProblem(syst2, u0, tspan) # problem to solve\n",
    "    sol = solve(prob, RK4(),reltol=1e-6, timeseries_steps = 6)\n",
    "\n",
    "    N = length(sol.u)\n",
    "    J = length(sol.u[1])\n",
    "\n",
    "    U = zeros(N, J)\n",
    "\n",
    "    for i in 1:N, j in 1:J\n",
    "        U[i,j] = sol.u[i][j] # перепишем ответы в матрицу по-удобней\n",
    "    end\n",
    "    U\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "Ans1 = predatorvictim2.( 1e-3, SC )\n",
    "Ans2 = predatorvictim2.( [1e-2 5e-3 1e-5] )\n",
    "\n",
    "p1 = ploter(Ans1)\n",
    "p2 = ploter(Ans2)\n",
    "\n",
    "set_default_plot_size(15cm, 8cm)\n",
    "\n",
    "hstack(p1,p2)"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "Фазовый портрет системы, имеет одну стационарную точку (аттрактор), на которую «накручивается» решение. В теории динамических систем аттрактор такого типа называется *фокусом*.\n",
    "\n",
    "К сожалению, попытка введения в модель «хищник-жертва» конкурентной борьбы среди жертв приводит к колебаниям, но, увы, быстро затухающим, что в реальных биологических сообществах не наблюдается."
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Julia 1.1.0",
   "language": "julia",
   "name": "julia-1.1"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.1.0"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 2
}
